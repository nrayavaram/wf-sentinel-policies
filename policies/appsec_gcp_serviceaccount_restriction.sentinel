# This policy uses the Sentinel tfplan/v2 import to require that
# all GCE resources following all the policies related to service account

# Note that the comparison is case-sensitive.

import "strings"
import "types"
import "tfplan-functions" as plan
import "generic-functions" as gen

# Filter to GCE instances with violations
# Warnings will be printed for all violations since the last parameter is true

//**********************Variables to be used***********************************
selected_node = null
default_compute_sa = "compute@developer.gserviceaccount.com"

messages = {}

# Resource Types Service Account Param Map
resourceTypesServiceAccountMap = {
	"google_dataproc_cluster": {
		"key": "cluster_config.0.gce_cluster_config.0.service_account",
	},
}

//******Following function is being used to validate the service account details as per the GCP resource's requirement
//address is the key and rc is the value of resource_changes in the mock of tfplan-v2 file
check_service_account_config = func(address, rc) {

	key = resourceTypesServiceAccountMap[rc.type]["key"]
	selected_node = plan.evaluate_attribute(rc, key)

	#print("result: " + plan.to_string(address) + " : " + plan.to_string(selected_node))
	if types.type_of(selected_node) is "null" or types.type_of(selected_node) is "undefined" {

		result = plan.evaluate_attribute(rc.change.after_unknown, key)
		#print("result_unknown: " + plan.to_string(address) + " : " + plan.to_string(result))

		if plan.to_string(result) is "null" or plan.to_string(result) is "undefined" {
			return address + " service is not having any service account, please assign it"
		} else {
			return null
		}

	} else {

		arr_sa = strings.split(selected_node, default_compute_sa)

		if length(arr_sa) > 1 {
			return "The service account of " + address + " service can not be a default compute service account, please change it"
		} else {
			return null
		}

	}
}

//*******************validating service account only***************************
messages_sa = {}
for resourceTypesServiceAccountMap as key_address, _ {
	# Get all the instances on the basis of type
	allResources = plan.find_resources(key_address)
	for allResources as address, rc {

		message = null
		message = check_service_account_config(address, rc)
		if types.type_of(message) is not "null" {

			gen.create_sub_main_key_list(messages, messages_sa, address)

			append(messages_sa[address], message)
			append(messages[address], message)
		}
	}
}

//********************************************************************************************************

SVC_ACCOUNT_CHECK = rule {
	length(messages_sa) is 0
}

# Main rule
print(messages)

main = rule { SVC_ACCOUNT_CHECK }
